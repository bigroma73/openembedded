#@TYPE: Machine
#@NAME: Dreambox DM7020HD
#@DESCRIPTION: Machine configuration for the Dreambox DM7020HD

#comment out the following two lines to use currently experimental kernel 2.6.3x
PREFERRED_VERSION_linux-dm7020hd = "2.6.18"
PREFERRED_VERSION_linux-libc-headers = "2.6.18"
PREFERRED_VERSION_wpa-supplicant = "0.5.10"

#needed for wpa-supplicant (0.6.9)
COMBINED_FEATURES_append = "${@base_contains('PREFERRED_VERSION_linux-dm7020hd', '2.6.18', '', 'nl80211', d)}"

#take care when you do changes on MACHINE_ESSENTIAL_EXTRA_RDEPENDS/RRECOMMENDS you have to increment the recipes/tasks/task-boot.bb PR

MACHINE_ESSENTIAL_EXTRA_RDEPENDS_2.6.18 = "\
	kernel-module-nls-iso8859-15 \
	kernel-module-nls-cp850 \
	kernel-module-nls-utf8 \
	v4l-dvb-module-dvb-core \
	v4l-dvb-module-stv0299 \
"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_2.6.18 = "\
	kernel-module-cifs \
	kernel-module-exportfs \
	kernel-module-ext2 \
	kernel-module-reiserfs \
	kernel-module-xfs \
"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS_2.6.3x = "\
	kernel-module-autofs4 \
	kernel-module-dvb-core \
	kernel-module-fb \
	kernel-module-sg \
	kernel-module-snd-pcm \
	kernel-module-snd \
	kernel-module-sr-mod \
	kernel-module-stv0299 \
	kernel-module-usbhid \
"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_2.6.3x = "\
	kernel-module-cdrom \
	kernel-module-cifs \
	kernel-module-exportfs \
	kernel-module-ext2 \
	kernel-module-reiserfs \
	kernel-module-xfs \
"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ${@base_contains('PREFERRED_VERSION_linux-dm7020hd', '2.6.18', '${MACHINE_ESSENTIAL_EXTRA_RDEPENDS_2.6.18}', '${MACHINE_ESSENTIAL_EXTRA_RDEPENDS_2.6.3x}', d)}
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS = ${@base_contains('PREFERRED_VERSION_linux-dm7020hd', '2.6.18', '${MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_2.6.18}', '${MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_2.6.3x}', d)}

KERNEL_EXTRA_CMD = ${@base_contains('PREFERRED_VERSION_linux-dm7020hd', '2.6.18', '--disable-compressor=lzo ', '', d)}

EXTRA_IMAGECMD_jffs2 = " -e 0x40000 -n -l"
IMAGE_CMD_jffs2 = "\
	mkfs.jffs2 \
		--root=${IMAGE_ROOTFS}/boot \
		--faketime \
		--disable-compressor=lzo \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		${EXTRA_IMAGECMD}; \
	sumtool \
		-i ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		-o ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2.sum \
		${EXTRA_IMAGECMD}; \
	rm -rf ${IMAGE_ROOTFS}/boot/*; \
	mkfs.jffs2 \
		--root=${IMAGE_ROOTFS} \
		--faketime \
		${KERNEL_EXTRA_CMD} \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		${EXTRA_IMAGECMD}; \
	sumtool \
		-i ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		-o ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2.sum \
		${EXTRA_IMAGECMD}; \
	buildimage --brcmnand --arch ${MACHINE} -e 0x40000 -f 0x10000000 -s 4096 \
		-b 0x100000:${STAGING_LIBDIR}/dreambox-secondstage/main.bin.gz \
		-d 0x700000:${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2.sum \
		-d 0xF800000:${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2.sum \
		> ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.nfi"

GLIBC_EXTRA_OECONF = "--disable-profile --with-tls --with-__thread"
TARGET_FPU = "hard"

MACHINE_FEATURES += "modem wifi dreambox-libpassthrough"

require conf/machine/include/dreambox-mipsel.inc
