#@TYPE: Machine
#@NAME: Dreambox DM500HD
#@DESCRIPTION: Machine configuration for the Dreambox DM500HD

#comment out the following two lines to use currently experimental kernel 2.6.3x
PREFERRED_VERSION_linux-dm500hd = "2.6.18"
PREFERRED_VERSION_linux-libc-headers = "2.6.18"
PREFERRED_VERSION_wpa-supplicant = "0.5.10"

#needed for wpa-supplicant (0.6.9)
COMBINED_FEATURES_append = "${@base_contains('PREFERRED_VERSION_linux-dm500hd', '2.6.18', '', 'nl80211', d)}"

#take care when you do changes on MACHINE_ESSENTIAL_EXTRA_RDEPENDS/RRECOMMENDS you have to increment the recipes/tasks/task-boot.bb PR

MACHINE_ESSENTIAL_EXTRA_RDEPENDS_2.6.18 = "\
	kernel-module-nls-iso8859-15 \
	kernel-module-nls-cp850 \
	kernel-module-nls-cp1251 \
	kernel-module-ppp-async \
	kernel-module-ppp-generic \
	kernel-module-usbserial \
	kernel-module-cdc-acm \
	kernel-module-crc-ccitt \
	kernel-module-slhc \
	kernel-module-nls-utf8 \
	v4l-dvb-module-dvb-core \
"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_2.6.18 = "\
	kernel-module-cifs \
	kernel-module-exportfs \
	kernel-module-ext2 \
	kernel-module-ntfs \
	kernel-module-reiserfs \
	kernel-module-xfs \
"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS_2.6.3x = "\
	kernel-module-autofs4 \
	kernel-module-dvb-core \
	kernel-module-fb \
	kernel-module-sg \
	kernel-module-snd-pcm \
	kernel-module-snd \
	kernel-module-sr-mod \
	kernel-module-usbhid \
"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_2.6.3x = "\
	kernel-module-cdrom \
	kernel-module-cifs \
	kernel-module-exportfs \
	kernel-module-ext2 \
	kernel-module-reiserfs \
	kernel-module-xfs \
"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ${@base_contains('PREFERRED_VERSION_linux-dm500hd', '2.6.18', '${MACHINE_ESSENTIAL_EXTRA_RDEPENDS_2.6.18}', '${MACHINE_ESSENTIAL_EXTRA_RDEPENDS_2.6.3x}', d)}
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS = ${@base_contains('PREFERRED_VERSION_linux-dm500hd', '2.6.18', '${MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_2.6.18}', '${MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_2.6.3x}', d)}

KERNEL_EXTRA_CMD = ${@base_contains('PREFERRED_VERSION_linux-dm500hd', '2.6.18', '--disable-compressor=lzo ', '', d)}

EXTRA_IMAGECMD_jffs2 = " -e 0x4000 -n -l"
IMAGE_CMD_jffs2 = "\
	mkfs.jffs2 \
		--root=${IMAGE_ROOTFS}/boot \
		--faketime \
		--disable-compressor=lzo \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		${EXTRA_IMAGECMD}; \
	rm -rf ${IMAGE_ROOTFS}/boot/*; \
	mkfs.jffs2 \
		--root=${IMAGE_ROOTFS} \
		--faketime \
		${KERNEL_EXTRA_CMD} \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		${EXTRA_IMAGECMD}; \
	buildimage --brcmnand --arch ${MACHINE} -e 0x4000 -f 0x4000000 -s 512 \
		-b 0x40000:${STAGING_LIBDIR}/dreambox-secondstage/main.bin.gz \
		-d 0x3C0000:${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		-d 0x3C00000:${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		> ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.nfi"

GLIBC_EXTRA_OECONF = "--disable-profile --with-tls --with-__thread"
TARGET_FPU = "hard"

MACHINE_FEATURES += "modem wifi dreambox-libpassthrough"

require conf/machine/include/dreambox-mipsel.inc
